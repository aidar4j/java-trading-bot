plugins {
  id 'java'
  id 'groovy'
}

group = 'alexsoroka'
sourceCompatibility = '1.8' // JAVA 8

// Add lombok annotation processor
configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

// Show passed, skipped and failed tests in output
test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "short"
  }
}

// Generate fat jar for application on build
jar {
  manifest {
    attributes 'Main-Class': 'alexsoroka.Main'
  }
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  // Reduces java boilerplate code
  implementation 'org.projectlombok:lombok:1.18.6'
  annotationProcessor 'org.projectlombok:lombok:1.18.6'
  // Logger
  implementation 'org.slf4j:slf4j-api:1.7.26'
  implementation 'ch.qos.logback:logback-classic:1.2.3'
  // Apache math utils
  implementation 'org.apache.commons:commons-math3:3.6.1'
  // Apache collections utils
  implementation 'org.apache.commons:commons-collections4:4.1'
  // Groovy language for Spock test framework
  testImplementation 'org.codehaus.groovy:groovy-all:2.5.6'
  // Testing and specification framework
  testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}
